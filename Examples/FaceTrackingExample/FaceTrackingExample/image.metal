//
//  image.metal
//  ComfyMark
//
//  Created by Aryan Rogye on 9/1/25.
//

#include <metal_stdlib>
using namespace metal;


// MARK: - Vertex Structs
struct VertexIn {
    float2 pos;
};

struct VertexOut {
    float4 position [[position]]; // required so rasterizer knows screen pos
    float2 texture_pos;
    float view_aspect;
    float4 color;                 // any extra varyings you want to interpolate
};

/*
 Swift Side:
 /// struct Viewport {
 ///    var origin: CGPoint = .zero
 ///    var scale: CGFloat = 1.0
 /// }
 
*/
struct Viewport {
    float2 origin;
    float scale;
    float view_aspect;
};

vertex VertexOut
vertexImageShader (
                   const device VertexIn* vertices [[buffer(0)]],
                   constant Viewport& vp [[buffer(1)]],
                   uint vid [[vertex_id]]
                   ) {
    /// new_coordinate = (old_coordinate + 1) / 2
    /// Cuz Texture Coordinate is from (0,0) to (1,1)
    /// But Vertex Goes From (-1,-1) to (1,1)
    
    VertexOut out;
    
    float2 vertex_pos = vertices[vid].pos;  // Original -1 to 1 coords
    /// Scaling based on the zoom or whatever the user chooses
    vertex_pos = (vertex_pos - vp.origin) * vp.scale;
    
    // Texture sampling position: apply the INVERSE transform so that
    // scale > 1 samples a smaller region (magnifies), and origin pans.
    float2 sample_pos = (vertex_pos / vp.scale) + vp.origin;
    // Convert to 0..1 texture coords and flip Y
    float2 tex_coords = (sample_pos + 1) / 2;
    tex_coords.y = 1.0 - tex_coords.y;
    
    out.texture_pos = tex_coords;
    out.view_aspect = vp.view_aspect;
    out.position = float4(vertex_pos, 0.0, 1.0);
    
    return out;
}

fragment float4
fragmentImageShader(
                    VertexOut in [[stage_in]],
                    texture2d<float> baseTex [[texture(0)]]
                    ) {
    constexpr sampler s(mag_filter::linear, min_filter::linear);
    
    float texAspect  = float(baseTex.get_width()) / float(baseTex.get_height());
    float viewAspect = max(in.view_aspect, 0.0001);
    
    float2 uv = in.texture_pos;
    float ratio = viewAspect / texAspect;
    
    if (ratio < 1.0) {
        float offset = (1.0 - ratio) * 0.5;
        uv.x = uv.x * ratio + offset;
    } else {
        float invRatio = 1.0 / ratio;
        float offset = (1.0 - invRatio) * 0.5;
        uv.y = uv.y * invRatio + offset;
    }
    
    float4 base = baseTex.sample(s, uv);
    return float4(base.rgb, 1.0);
}
